//============================================================
// USERID:........ KMANZANA
// PROGRAMMER:.... Manzanares, Kelton M.
// COURSE:........ CSCI-410
// TERM:.......... SP14
// PROJECT:....... 03
// FILENAME:...... PC.hdl
//============================================================

// This file is based on a file from www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
  IN in[16], load, inc, reset;
  OUT out[16];

  PARTS:
  Inc16(in=Q, out=incremented);

  Mux8Way16(a=Q, b=false, c=in, d=false, e=incremented, f=false, g=in, h=false,
    sel[0]=reset, sel[1]=load, sel[2]=inc, out=D);

  Register(in=D, load=true, out=Q, out=out);
}
