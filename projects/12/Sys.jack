//============================================================
// USERID:......... KMANZANA
// PROGRAMMER:..... Manzanares, Kelton M.
// COURSE:......... CSCI-410
// TERM:........... SP14
// PROJECT:........ ECS12
// FILENAME:....... Sys.jack
//============================================================

// This file is based on a file from www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library of basic system services.
 */
class Sys {
  /** Performs all the initializations required by the OS. */
  function void init() {
    do Keyboard.init();
    do Math.init();
    do Memory.init();
    do Output.init();
    do Screen.init();

    do Main.main();

    do Sys.halt();

    return;
  }

  /** Halts execution. */
  function void halt() {
    while (true) {}

    return;
  }

  /** Waits approximately duration milliseconds and then returns. */
  function void wait(int duration) {
    var int i, j;

    if (duration < 0) {
      do Sys.error(1); // Sys.wait: Duration must be positive
    }

    let i = 0;

    while (i < duration) {
      let j = 0;

      while (j < 10) {
        do Memory.peek(0);
        do Memory.peek(0);
        do Memory.peek(0);

        let j = j + 1;
      }

      let i = i + 1;
    }

    return;
  }

  /** Prints the given error code in the form "ERR<errorCode>", and halts. */
  function void error(int errorCode) {
    do Output.printString("ERR");
    do Output.printInt(errorCode);

    do Sys.halt();

    return;
  }
}
