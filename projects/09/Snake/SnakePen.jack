//============================================================
// USERID:......... KMANZANA
// PROGRAMMER:..... Manzanares, Kelton M.
// COURSE:......... CSCI-410
// TERM:........... SP14
// PROJECT:........ ECS09
// FILENAME:....... SnakePen.jack
//============================================================

/* Class for SnakePen implementation...
   enclosure for snake and information */
class SnakePen {
  static char up, right, down, left;
  field int xMin, yMin, xMax, yMax;

  constructor SnakePen setSize(int setXMin, int setYMin, int setXMax, int setYMax) {
    let up    = 131;
    let right = 132;
    let down  = 133;
    let left  = 130;

    let xMin = setXMin;
    let yMin = setYMin;
    let xMax = setXMax;
    let yMax = setYMax;

    return this;
  }

  method int nextXPosition(int position, char direction) {
    if (direction = right) {
      if (position = xMax) {
        return xMin;
      } else {
        return position + 1;
      }
    }
    if (direction = left) {
      if (position = xMin) {
        return xMax;
      } else {
        return position - 1;
      }
    }

    return position;
  }

  method int nextYPosition(int position, char direction) {
    if (direction = down) {
      if (position = yMax) {
        return yMin;
      } else {
        return position + 1;
      }
    }
    if (direction = up) {
      if (position = yMin) {
        return yMax;
      } else {
        return position - 1;
      }
    }

    return position;
  }

  method int getYMid() {
    return yMax / 2;
  }

  method int getXMax() {
    return Math.max(xMax, xMin);
  }

  function char directionMin() {
    var char rl, ud;
    let rl = Math.min(right, left);
    let ud = Math.min(up, down);

    return Math.min(rl, ud);
  }

  function char directionMax() {
    return down;
  }

  function char initialDirection() {
    return right;
  }
}
